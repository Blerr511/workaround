load("@npm//:defs.bzl", "npm_link_all_packages")
load("@aspect_rules_js//js:defs.bzl", "js_binary", "js_run_devserver", "js_test")
load("@aspect_rules_ts//ts:defs.bzl", "ts_config", "ts_project")

service_name = "data-backend"

package_name = "server/backend"

npm_link_all_packages(name = "node_modules")

filegroup(
    name = "project_support_files",
    srcs = glob([
        "*.json",
        ".*.json",
        "*.js",
        ".*.js",
    ]),
)

srcs = glob(
    [
        "src/**/*.ts",
        "test/**/*.ts",
        "migration/**/*.ts",
    ],
)

data = ["project_support_files"]

deps = [
    ":node_modules",
    "//:node_modules/@wr/logger",
    "//:node_modules/@wr/data-source",
]

ts_config(
    name = "tsconfig",
    src = "tsconfig.json",
)

ts_project(
    name = "ts",
    srcs = srcs,
    data = data,
    declaration = False,
    incremental = False,
    source_map = True,
    tsconfig = ":tsconfig",
    validate = True,
    deps = deps,
)

js_binary(
    name = "start",
    data = [
        ":ts",
    ],
    entry_point = ":src/main.js",
    env = {"DATA_SOURCE_POSTGRES_URL": "$(DATA_SOURCE_POSTGRES_URL)"},
    toolchains = [
        "//sandbox:data_source_env_vars",
    ],
)

js_binary(
    name = "run_schema_generator",
    data = [":ts"],
    entry_point = ":src/cli/generate-schema.js",
)

genrule(
    name = "schema",
    srcs = [":ts"],
    outs = ["schema.gql"],  # the output file
    cmd = """
    export BAZEL_BINDIR="."
    $(execpath :run_schema_generator)
    cp schema.gql $@
    """,
    tools = [":run_schema_generator"],
    visibility = ["//visibility:public"],
)
