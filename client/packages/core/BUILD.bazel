load("@aspect_rules_js//npm:defs.bzl", "npm_package")
load("@aspect_rules_swc//swc:defs.bzl", "swc")
load("@bazel_skylib//lib:partial.bzl", "partial")
load("@aspect_rules_ts//ts:defs.bzl", "ts_config", "ts_project")
load("@bazel_skylib//lib:partial.bzl", "partial")
load("@npm//:defs.bzl", "npm_link_all_packages")

npm_link_all_packages(name = "node_modules")

ts_config(
    name = "tsconfig",
    src = "tsconfig.json",
)

ts_project(
    name = "ts",
    srcs = glob(["src/**"]),
    allow_js = True,
    data = [
        ":tsconfig",
    ],
    declaration = True,
    incremental = True,
    preserve_jsx = True,
    resolve_json_module = True,
    source_map = True,
    transpiler = partial.make(
        swc,
        args = ["--env-name=prod"],
        source_maps = "true",
        swcrc = ".swcrc",
    ),
    validate = True,
    deps = [
        ":node_modules",
    ],
)

genrule(
    name = "prod_package_json",
    srcs = ["package.json"],
    outs = ["prod_package.json"],
    cmd = """
    jq '.main = "src/index.js"' $(location package.json) > $@
    """,
)

# make this library available via node_modules
npm_package(
    name = "core",
    srcs = [
        ":prod_package_json",
        ":ts",
        ":ts_typings",
    ],
    # This is a perf improvement; the default will be flipped to False in rules_js 2.0
    include_runfiles = False,
    replace_prefixes = {"prod_package.json": "package.json"},
    visibility = ["//visibility:public"],
)

sh_binary(
    name = "npm.publish",
    srcs = [
        "//tools/npm:npm_cli",
    ],
    args = [
        "publish",
        "$(location :core)",
        # "--dry-run",
    ],
    data = [
        ":core",
        "//tools/gcloud:npmrc",
    ],
    env = {
        "NPM_CONFIG_USERCONFIG": "$(location //tools/gcloud:npmrc)",
    },
    tags = ["requires-network"],
)

filegroup(
    name = "core.json",
    srcs = [":package.json"],
    visibility = ["//visibility:public"],
)
